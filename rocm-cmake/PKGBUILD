# Maintainer: Markus NÃ¤ther <naetherm@cs.uni-freiburg.de>
# Contributor: fermyon <antifermion@protonmail.com>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail.com>

pkgname=rocm-cmake
pkgver=3.7.0
pkgrel=1
pkgdesc='CMake modules for common build tasks needed for the ROCm software stack'
arch=('any')
url='https://github.com/RadeonOpenCompute/rocm-cmake'
license=('MIT')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/rocm-$pkgver.tar.gz"
        'ROCMToolchain.cmake')
sha256sums=('51abfb06124c2e0677c4d6f7fe83c22fe855cb21386f0053ace09f8ab297058b'
            'c28f87fa878f9717b8fda6afb0786cf8463377a1a28e2981a45118c35b6ad4cd')
_dirname="$(basename "$url")-$(basename "${source[0]}" .tar.gz)"

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/opt/rocm "$_dirname"
}

package() {
  make DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir/opt/rocm/share/rocm/cmake" ROCMToolchain.cmake
  install -Dm644 "$_dirname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
