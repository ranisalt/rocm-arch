# Maintainer: Ranieri Althoff <ranisalt+aur at gmail dot com>

_opencl_icd_loader_commit='978b4b3a29a3aebc86ce9315d5c5963e88722d03'

pkgname=rocm-opencl-runtime
pkgver=3.1.0
pkgrel=2
pkgdesc='Radeon Open Compute - OpenCL runtime'
license=('MIT')
arch=('x86_64')
url='https://github.com/RadeonOpenCompute/ROCm-OpenCL-Runtime'
depends=("hsakmt-roct>=$pkgver" "rocm-comgr>=$pkgver" "rocr-runtime>=$pkgver" 'opencl-icd-loader')
makedepends=(mesa cmake git llvm-roc rocm-cmake)
provides=('opencl-driver')
source=(
    "$url/archive/roc-$pkgver.tar.gz"
    "https://github.com/KhronosGroup/OpenCL-ICD-Loader/archive/$_opencl_icd_loader_commit.tar.gz"
    # "rocm-opencl-runtime-2.8.0-change-AMDCompilerh.patch"
    "rocm-opencl-runtime-2.8.0-change-opencl.patch"
    "rocm-opencl-runtime-2.8.0-amdocl64icd.patch"
    "rocm-opencl-runtime-3.0.0-change-install-location.patch"
)

sha256sums=('c14f544daef32e991af4f356825d1e9cb99ddad4a58ba7dda6054603a9c5d1b8'
            '0c14bf890bd198ef5a814b5b7ed57b69e890b0c0a1bcfba8fdad996fa1a97fc7'
            '3af5c9c3b8b88b78a2fd574f339e88a5cd62c365d94e9289c2a2cb4afef3d435'
            '2cfd11bda9a485d6de2231c56742ad553329cab9b6dcc009dbddbcde1436f485'
            '941a29f8704a2839c32bcf3cf374dde30bc8a839c1136d4faa65c60a7500cf98')

_dirname="ROCm-OpenCL-Runtime-roc-$pkgver"

prepare() {
  cd "$_dirname"

  # [ -d tools/clinfo ] && rm -rf tools/clinfo

  mkdir -p api/opencl/khronos
  mv "$srcdir/OpenCL-ICD-Loader-$_opencl_icd_loader_commit" api/opencl/khronos/icd

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 -i "$srcdir/$src"
  done
}

build() {
  cmake \
    -DCMAKE_INSTALL_PREFIX=/opt/rocm \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_MODULE_PATH="$srcdir/rocm-cmake/share/rocm/cmake" \
    -DCMAKE_PREFIX_PATH=/opt/rocm/lib/cmake \
    -DLLVM_DIR=/opt/rocm/lib/cmake/llvm \
    -DUSE_COMGR_LIBRARY=yes \
    "$_dirname"
  make
}

package() {
  DESTDIR="$pkgdir/" make install

  install -Dm644 "$_dirname/License" "$pkgdir/usr/share/licenses/rocm-opencl-runtime/LICENSE"
  install -d "$pkgdir/etc/ld.so.conf.d"
  cat << EOF > "$pkgdir/etc/ld.so.conf.d/$pkgname.conf"
/opt/rocm/lib
EOF
}
