# Maintainer: Ranieri Althoff <ranisalt+aur at gmail dot com>

pkgname=rocm-device-libs
pkgver=3.1.0
pkgrel=3
pkgdesc='Radeon Open Compute - device libs'
license=('custom:NCSAOSL')
arch=('x86_64')
url='https://github.com/RadeonOpenCompute/ROCm-Device-Libs'
makedepends=(cmake llvm-roc rocm-cmake)
source=("$url/archive/roc-ocl-$pkgver.tar.gz")
sha256sums=('bce5da4adba4ea360cd4a8aff39d25d0d8a900bdd13aff0482b685bb495508f6')

_dirname="ROCm-Device-Libs-roc-ocl-$pkgver"

build() {
  cd "$srcdir/$_dirname/utils"
  cmake -DLLVM_DIR=/opt/rocm/lib/cmake/llvm .
  make

  cd "$srcdir"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/opt/rocm \
    -DLLVM_DIR=/opt/rocm/lib/cmake/llvm \
    -DPREPARE_BUILTINS="$srcdir/$_dirname/utils/prepare-builtins/prepare-builtins" \
    -DROCM_DIR=/opt/rocm/share/rocm/cmake \
    "$_dirname"
  make
}

package() {
    DESTDIR="$pkgdir/" make install

    install -Dm644 "$_dirname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
